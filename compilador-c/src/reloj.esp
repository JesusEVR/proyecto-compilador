//  RELOJ ANALÓGICO

// Estado inicial del reloj
variable ENTERO horas = 0;
variable ENTERO minutos = 0;

imprimir(">>> Configuración Inicial <<<");

imprimir("Ingresa la hora (1-12):");
horas = entrada();

imprimir("Ingresa los minutos (un múltiplo de 5, entre 0 y 60):");
minutos = entrada();

// Validaciones de seguridad
Si (horas > 12) { horas = 12; }
Si (horas < 1)  { horas = 1; }
Si (minutos > 59) { minutos = 0; }
Si (minutos < 0)  { minutos = 0; }

// Variables de centro
variable ENTERO cx = 32;
variable ENTERO cy = 32;

// Cerrojos para botones
variable ENTERO lock_min = 0;
variable ENTERO lock_hor = 0;

// Bandera de actualización
variable ENTERO update = 1;

// Inputs
variable ENTERO key_1 = 0; // Hora
variable ENTERO key_2 = 0; // Minuto

imprimir("-> Reloj analógico iniciado");
imprimir("-> Flecha Abajo para horas y Flecha Izquierda para minutos");

// Bucle Principal
Mientras (1) {

// Lectura de las reglas
leer_tecla(1, key_1); // Horas
leer_tecla(2, key_2); // Minutos

// Control de las horas
Si (key_1 == 1) {
    Si (lock_hor == 0) {
        horas = horas + 1;
        Si (horas > 12) { horas = 1; }
        
        imprimir(">> Hora:"); 
        imprimir(horas);
        
        update = 1;
        lock_hor = 1;
    }
}
Si (key_1 == 0) { lock_hor = 0; }

// Control de los minutos
Si (key_2 == 1) {
    Si (lock_min == 0) {
        minutos = minutos + 5; 
        Si (minutos >= 60) { minutos = 0; }
        
        imprimir(">> Minuto:");
        imprimir(minutos);
        
        update = 1;
        lock_min = 1;
    }
}
Si (key_2 == 0) { lock_min = 0; }


// Renderizado
Si (update == 1) {
    
    // Borrar pantalla
    variable ENTERO by = 0;
    Mientras (by < 64) {
        variable ENTERO bx = 0;
        Mientras (bx < 64) {
            pintar_pixel(bx, by, 0);
            bx = bx + 1;
        }
        by = by + 1;
    }

    // Dibujamos marco
    variable ENTERO border = 2;
    Mientras (border < 62) {
        pintar_pixel(border, 2, 1);
        pintar_pixel(border, 61, 1);
        pintar_pixel(2, border, 1);
        pintar_pixel(61, border, 1);
        border = border + 1;
    }
    
    // Dibujamos marcas
    pintar_pixel(32, 4, 1);  // 12
    pintar_pixel(32, 60, 1); // 6
    pintar_pixel(60, 32, 1); // 3
    pintar_pixel(4, 32, 1);  // 9
    
    pintar_pixel(46, 8, 1);  // 1
    pintar_pixel(56, 18, 1); // 2
    pintar_pixel(56, 46, 1); // 4
    pintar_pixel(46, 56, 1); // 5
    pintar_pixel(18, 56, 1); // 7
    pintar_pixel(8, 46, 1);  // 8
    pintar_pixel(8, 18, 1);  // 10
    pintar_pixel(18, 8, 1);  // 11
    pintar_pixel(32, 32, 1); // Centro

    // Calculo de las coordenadas

    // Horas
    variable ENTERO hx = 32; 
    variable ENTERO hy = 32;
    
    Si (horas == 12) { hx = 32; hy = 20; } 
    Si (horas == 1)  { hx = 38; hy = 22; } 
    Si (horas == 2)  { hx = 42; hy = 26; }
    Si (horas == 3)  { hx = 44; hy = 32; } 
    Si (horas == 4)  { hx = 42; hy = 38; }
    Si (horas == 5)  { hx = 38; hy = 42; }
    Si (horas == 6)  { hx = 32; hy = 44; } 
    Si (horas == 7)  { hx = 26; hy = 42; }
    Si (horas == 8)  { hx = 22; hy = 38; }
    Si (horas == 9)  { hx = 20; hy = 32; } 
    Si (horas == 10) { hx = 22; hy = 26; }
    Si (horas == 11) { hx = 26; hy = 22; } 

    // Minutos
    variable ENTERO mx = 32; 
    variable ENTERO my = 32;
    variable ENTERO midx = minutos / 5;
    
    Si (midx == 0) { mx = 32; my = 12; } 
    Si (midx == 1) { mx = 42; my = 15; }
    Si (midx == 2) { mx = 49; my = 22; }
    Si (midx == 3) { mx = 52; my = 32; } 
    Si (midx == 4) { mx = 49; my = 42; }
    Si (midx == 5) { mx = 42; my = 49; }
    Si (midx == 6) { mx = 32; my = 52; } 
    Si (midx == 7) { mx = 22; my = 49; }
    Si (midx == 8) { mx = 15; my = 42; }
    Si (midx == 9) { mx = 12; my = 32; } 
    Si (midx == 10){ mx = 15; my = 22; }
    Si (midx == 11){ mx = 22; my = 15; }
    Si (midx == 12){ mx = 32; my = 12; }

    // Trazado de líneas
    
    // Dibujamos manecilla de horas
    variable ENTERO x0 = 32; variable ENTERO y0 = 32;
    variable ENTERO x1 = hx; variable ENTERO y1 = hy;
    
    variable ENTERO dx = x1 - x0;
    variable ENTERO dy = y1 - y0;
    variable ENTERO sx = 1; variable ENTERO sy = 1;
    
    Si (dx < 0) { dx = 0 - dx; sx = 0 - 1; }
    Si (dy < 0) { dy = 0 - dy; sy = 0 - 1; }
    
    variable ENTERO err = dx - dy;
    variable ENTERO e2 = 0;
    variable ENTERO loop = 1;
    
    Mientras (loop == 1) {
        pintar_pixel(x0, y0, 1);
        Si (x0 == x1 Y y0 == y1) { loop = 0; }
        
        e2 = 2 * err;
        variable ENTERO ndy = 0 - dy;
        Si (e2 > ndy) { err = err - dy; x0 = x0 + sx; }
        Si (e2 < dx) { err = err + dx; y0 = y0 + sy; }
    }

    // Dibujamos manecilla de minutos
    x0 = 32; y0 = 32;
    x1 = mx; y1 = my;
    
    dx = x1 - x0;
    dy = y1 - y0;
    sx = 1; sy = 1;
    
    Si (dx < 0) { dx = 0 - dx; sx = 0 - 1; }
    Si (dy < 0) { dy = 0 - dy; sy = 0 - 1; }
    
    err = dx - dy;
    loop = 1;
    
    Mientras (loop == 1) {
        pintar_pixel(x0, y0, 1);
        Si (x0 == x1 Y y0 == y1) { loop = 0; }
        
        e2 = 2 * err;
        variable ENTERO ndy2 = 0 - dy;
        Si (e2 > ndy2) { err = err - dy; x0 = x0 + sx; }
        Si (e2 < dx) { err = err + dx; y0 = y0 + sy; }
    }

    update = 0; 
}

// Retardo mínimo para fluidez
variable ENTERO i = 0;
Mientras (i < 50) { i = i + 1; }

}